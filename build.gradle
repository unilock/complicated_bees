plugins {
	id 'fabric-loom' version '1.8.+'
	id 'maven-publish'
}

version = "${project.mod_version}+${project.minecraft_version}+fabric"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = "ParchmentMC"
		url = "https://maven.parchmentmc.org/"
	}
	maven {
		name = "BlameJared"
		url = "https://maven.blamejared.com/"
	}
	exclusiveContent {
		forRepository {
			maven {
				name = "Cursemaven"
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
}

loom {
	mods {
		"complicated_bees" {
			sourceSet sourceSets.main
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.layered {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-${minecraft_version}:${parchment_mappings}@zip"
	}
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
	modImplementation "curse.maven:jade-324717:${jade_id}"

	modCompileOnlyApi "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
	modCompileOnlyApi "mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}"

	modRuntimeOnly "mezz.jei:jei-${minecraft_version}-fabric:${jei_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

//jar {
//	from("LICENSE") {
//		rename { "${it}_${base.archivesName.get()}" }
//	}
//}

// Configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
